require "../../spec_helper"

class TrackingClient < HTTP::Client
  getter? explicitly_closed : Bool = false

  def close : Nil
    @explicitly_closed = true
    super
  end
end

# Expose private methods for testing via a thin subclass.
class TelegramChannelTest < Autobot::Channels::TelegramChannel
  getter created_clients = [] of TrackingClient
  property stubbed_file_path : String? = nil
  property target_uri : URI = URI.parse(TELEGRAM_API_BASE)

  protected def build_api_client : HTTP::Client
    client = TrackingClient.new(@target_uri)
    client.connect_timeout = Autobot::HTTP::REQUEST_CONNECT_TIMEOUT
    client.read_timeout = Autobot::HTTP::DEFAULT_READ_TIMEOUT
    @created_clients << client
    client
  end

  private def api_request(method : String, params : Hash(String, String) = {} of String => String) : JSON::Any?
    if method == "getFile" && (fp = @stubbed_file_path)
      return JSON.parse(%({"file_path": "#{fp}", "file_size": 100}))
    end
    super
  end

  property stubbed_file_bytes : Bytes? = nil

  private def download_telegram_file_bytes(file_id : String) : Bytes?
    @stubbed_file_bytes || super
  end

  def test_extract_sender(msg : JSON::Any) : Sender?
    extract_sender(msg)
  end

  def test_bot_username=(name : String)
    @bot_username = name
  end

  def test_command_for_me?(text : String, msg : JSON::Any) : Bool
    sender = extract_sender(msg)
    raise "no sender" unless sender
    command_for_me?(text, msg, sender)
  end

  def test_service_message?(msg : JSON::Any) : Bool
    service_message?(msg)
  end

  def test_addressed?(msg : JSON::Any) : Bool
    sender = extract_sender(msg)
    sender ? addressed?(msg, sender) : false
  end

  def test_chat_params(chat_id : String) : Hash(String, String)
    chat_params(chat_id)
  end

  def test_access_denied_message(sender_id : String) : String
    access_denied_message(sender_id)
  end

  def test_build_content_and_media(msg : JSON::Any) : {String, Array(Autobot::Bus::MediaAttachment)}
    build_content_and_media(msg)
  end

  def test_command_description(entry : Autobot::Config::CustomCommandEntry, name : String) : String
    command_description(entry, name)
  end

  def test_format_cron_job_html(job : Autobot::Cron::CronJob, index : Int32) : String
    format_cron_job_html(job, index)
  end

  def test_find_photo_attachment(media : Array(Autobot::Bus::MediaAttachment)?) : Autobot::Bus::MediaAttachment?
    find_photo_attachment(media)
  end

  def test_find_sendable_attachment(media : Array(Autobot::Bus::MediaAttachment)?) : Autobot::Bus::MediaAttachment?
    find_sendable_attachment(media)
  end

  def test_build_photo_multipart(chat_id : String, photo_bytes : Bytes, caption : String) : String
    build_photo_multipart(chat_id, photo_bytes, caption)
  end

  def test_build_media_multipart(chat_id : String, file_bytes : Bytes, caption : String, field_name : String, filename : String, content_type : String) : String
    build_media_multipart(chat_id, file_bytes, caption, field_name: field_name, filename: filename, content_type: content_type)
  end

  def test_extract_reply_context(msg : JSON::Any) : String?
    extract_reply_context(msg)
  end

  def test_prepend_reply_context(content : String, reply_text : String?) : String
    prepend_reply_context(content, reply_text)
  end

  def test_media_filename(attachment : Autobot::Bus::MediaAttachment, default : String) : String
    media_filename(attachment, default)
  end

  def test_parse_script_args(args_str : String) : Array(String)
    parse_script_args(args_str)
  end

  def test_execute_script(script_path : String, args : String, chat_id : String) : Nil
    execute_script(script_path, args, chat_id)
  end

  def test_apply_proxy(client : HTTP::Client) : Nil
    if proxy_url = @proxy
      Autobot::HTTP.apply_proxy(client, proxy_url)
    end
  end

  def test_api_request(method : String, params : Hash(String, String) = {} of String => String) : JSON::Any?
    api_request(method, params)
  end

  def test_api_get(method : String, params : Hash(String, String) = {} of String => String) : JSON::Any?
    api_get(method, params)
  end

  def test_download_telegram_file_bytes(file_id : String) : Bytes?
    download_telegram_file_bytes(file_id)
  end

  def test_send_media_request(chat_id : String, file_bytes : Bytes, caption : String, api_method : String, field_name : String, filename : String, content_type : String) : Nil
    send_media_request(chat_id, file_bytes, caption,
      api_method: api_method, field_name: field_name, filename: filename, content_type: content_type)
  end

  getter sent_replies = [] of String

  private def send_reply(chat_id : String, text : String) : Nil
    sent_replies << text
  end

  private def start_typing(chat_id : String) : Nil
  end

  private def stop_typing(chat_id : String) : Nil
  end
end

private def build_channel(
  allow_from : Array(String) = [] of String,
  custom_commands : Autobot::Config::CustomCommandsConfig? = nil,
  cron_service : Autobot::Cron::Service? = nil,
  proxy : String? = nil,
) : TelegramChannelTest
  bus = Autobot::Bus::MessageBus.new
  cmds = custom_commands || Autobot::Config::CustomCommandsConfig.new
  TelegramChannelTest.new(
    bus: bus,
    token: "test-token",
    allow_from: allow_from,
    proxy: proxy,
    custom_commands: cmds,
    cron_service: cron_service,
  )
end

private TOPIC_MESSAGE   = %({"message_id": 9, "message_thread_id": 57, "is_topic_message": true, "chat": {"id": -1001, "type": "supergroup"}, "from": {"id": 1, "first_name": "Ann"}, "text": "hi"})
private GENERAL_REPLY   = %({"message_id": 9, "message_thread_id": 3, "chat": {"id": -1001, "type": "supergroup"}, "from": {"id": 1, "first_name": "Ann"}, "text": "hi"})
private PRIVATE_MESSAGE = %({"message_id": 1, "chat": {"id": 5, "type": "private"}, "from": {"id": 1, "first_name": "Ann"}, "text": "/help"})
private GENERAL_MESSAGE = %({"message_id": 9, "chat": {"id": -1001, "type": "supergroup", "is_forum": true}, "from": {"id": 1, "first_name": "Ann"}, "text": "hi"})

describe Autobot::Channels::TelegramChannel do
  describe "forum topics" do
    it "folds the topic into the chat id" do
      channel = TelegramChannelTest.new(bus: Autobot::Bus::MessageBus.new, token: "t")
      sender = channel.test_extract_sender(JSON.parse(TOPIC_MESSAGE))
      sender.try(&.[:chat_id]).should eq("-1001:57")
      sender.try(&.[:topic]).should eq(57)
      channel.test_extract_sender(JSON.parse(GENERAL_REPLY)).try(&.[:chat_id]).should eq("-1001")
      channel.test_extract_sender(JSON.parse(GENERAL_MESSAGE)).try(&.[:chat_id]).should eq("-1001:1")
    end

    it "ignores service messages such as a member joining or a topic being created" do
      channel = TelegramChannelTest.new(bus: Autobot::Bus::MessageBus.new, token: "t", topics: [1_i64])
      joined = %({"message_id": 10, "chat": {"id": -1001, "type": "supergroup", "is_forum": true}, "from": {"id": 1, "first_name": "Ann"}, "new_chat_members": [{"id": 2, "is_bot": true, "first_name": "Bot"}]})
      created = %({"message_id": 11, "message_thread_id": 5, "is_topic_message": true, "chat": {"id": -1001, "type": "supergroup", "is_forum": true}, "from": {"id": 1, "first_name": "Ann"}, "forum_topic_created": {"name": "memo", "icon_color": 1}})
      channel.test_service_message?(JSON.parse(joined)).should be_true
      channel.test_service_message?(JSON.parse(created)).should be_true
      channel.test_service_message?(JSON.parse(GENERAL_MESSAGE)).should be_false
      channel.test_service_message?(JSON.parse(TOPIC_MESSAGE)).should be_false
    end

    it "handles a group command only when it names this bot or lands in an owned topic" do
      channel = TelegramChannelTest.new(bus: Autobot::Bus::MessageBus.new, token: "t", topics: [57_i64])
      channel.test_bot_username = "mybot"
      channel.test_command_for_me?("/help@mybot", JSON.parse(GENERAL_REPLY)).should be_true
      channel.test_command_for_me?("/help@MyBot", JSON.parse(GENERAL_REPLY)).should be_true
      channel.test_command_for_me?("/help@otherbot", JSON.parse(TOPIC_MESSAGE)).should be_false
      channel.test_command_for_me?("/help", JSON.parse(TOPIC_MESSAGE)).should be_true
      channel.test_command_for_me?("/help", JSON.parse(GENERAL_REPLY)).should be_false
      channel.test_command_for_me?("/help", JSON.parse(PRIVATE_MESSAGE)).should be_true
    end

    it "owns the General topic of a forum as topic 1" do
      channel = TelegramChannelTest.new(bus: Autobot::Bus::MessageBus.new, token: "t", topics: [1_i64])
      channel.test_addressed?(JSON.parse(GENERAL_MESSAGE)).should be_true
      channel.test_addressed?(JSON.parse(TOPIC_MESSAGE)).should be_false
      channel.test_chat_params("-1001:1").should eq({"chat_id" => "-1001"})
    end

    it "answers in its own topics without a mention and stays silent elsewhere" do
      channel = TelegramChannelTest.new(bus: Autobot::Bus::MessageBus.new, token: "t", topics: [57_i64])
      channel.test_addressed?(JSON.parse(TOPIC_MESSAGE)).should be_true
      channel.test_addressed?(JSON.parse(TOPIC_MESSAGE.sub("57", "58"))).should be_false
      channel.test_addressed?(JSON.parse(GENERAL_REPLY)).should be_false
    end

    it "sends the thread id with every request that has a topic" do
      channel = TelegramChannelTest.new(bus: Autobot::Bus::MessageBus.new, token: "t")
      channel.test_chat_params("-1001:57").should eq({"chat_id" => "-1001", "message_thread_id" => "57"})
      channel.test_chat_params("-1001").should eq({"chat_id" => "-1001"})
      body = channel.test_build_media_multipart("-1001:57", "x".to_slice, "c", "photo", "p.jpg", "image/jpeg")
      body.should contain(%(name="message_thread_id"\r\n\r\n57))
    end
  end

  describe "#access_denied_message" do
    it "shows setup instructions when allow_from is empty" do
      channel = build_channel(allow_from: [] of String)
      msg = channel.test_access_denied_message("12345|johndoe")

      msg.should contain("no authorized users yet")
      msg.should contain("allow_from")
      msg.should contain("config.yml")
      msg.should contain("12345|johndoe")
    end

    it "escapes HTML in sender ID" do
      channel = build_channel(allow_from: [] of String)
      msg = channel.test_access_denied_message("<script>alert(1)</script>")

      msg.should_not contain("<script>")
      msg.should contain("&lt;script&gt;")
    end

    it "shows generic denial when allow_from has users" do
      channel = build_channel(allow_from: ["allowed_user"])
      msg = channel.test_access_denied_message("other_user")

      msg.should contain("Access denied")
      msg.should contain("not in the authorized users list")
      msg.should_not contain("config.yml")
    end
  end

  describe "#command_description" do
    it "returns description when provided" do
      entry = Autobot::Config::CustomCommandEntry.new("prompt text", "My description")
      channel = build_channel
      channel.test_command_description(entry, "cmd").should eq("My description")
    end

    it "humanizes command name when no description" do
      entry = Autobot::Config::CustomCommandEntry.new("prompt text")
      channel = build_channel
      channel.test_command_description(entry, "check_status").should eq("Check status")
    end

    it "humanizes command name with hyphens" do
      entry = Autobot::Config::CustomCommandEntry.new("prompt text")
      channel = build_channel
      channel.test_command_description(entry, "run-deploy").should eq("Run deploy")
    end
  end

  describe "#format_cron_job_html" do
    it "formats a complete job entry" do
      tmp = TestHelper.tmp_dir
      cron = Autobot::Cron::Service.new(store_path: tmp / "cron.json")
      channel = build_channel(cron_service: cron)

      job = Autobot::Cron::CronJob.new(
        id: "abc123",
        name: "Stars check",
        schedule: Autobot::Cron::CronSchedule.new(kind: Autobot::Cron::ScheduleKind::Every, every_ms: 600_000_i64),
        payload: Autobot::Cron::CronPayload.new(message: "Check GitHub stars"),
      )

      result = channel.test_format_cron_job_html(job, 1)
      result.should contain("<b>1.</b>")
      result.should contain("abc123")
      result.should contain("Stars check")
      result.should contain("⏱ Every 10 min")
      result.should contain("⏳ pending")
      result.should contain("🤖")
      result.should contain("Check GitHub stars")
    ensure
      FileUtils.rm_rf(tmp) if tmp
    end

    it "produces output that splits within Telegram limits" do
      tmp = TestHelper.tmp_dir
      cron = Autobot::Cron::Service.new(store_path: tmp / "cron.json")
      channel = build_channel(cron_service: cron)

      lines = ["<b>Scheduled jobs (20)</b>"]
      20.times do |i|
        job = Autobot::Cron::CronJob.new(
          id: "job#{i}",
          name: "A long job name for testing #{"x" * 20}",
          schedule: Autobot::Cron::CronSchedule.new(kind: Autobot::Cron::ScheduleKind::Every, every_ms: 600_000_i64),
          payload: Autobot::Cron::CronPayload.new(message: "Detailed instruction " * 10),
        )
        lines << channel.test_format_cron_job_html(job, i + 1)
      end

      text = lines.join("\n\n")
      text.size.should be > Autobot::Channels::MarkdownToTelegramHTML::TELEGRAM_MAX_LENGTH

      chunks = Autobot::Channels::MarkdownToTelegramHTML.split_message(text)
      chunks.size.should be > 1
      chunks.each { |chunk| chunk.size.should be <= Autobot::Channels::MarkdownToTelegramHTML::TELEGRAM_MAX_LENGTH }
    ensure
      FileUtils.rm_rf(tmp) if tmp
    end

    it "escapes HTML in job name and message" do
      tmp = TestHelper.tmp_dir
      cron = Autobot::Cron::Service.new(store_path: tmp / "cron.json")
      channel = build_channel(cron_service: cron)

      job = Autobot::Cron::CronJob.new(
        id: "x1",
        name: "<script>alert</script>",
        schedule: Autobot::Cron::CronSchedule.new(kind: Autobot::Cron::ScheduleKind::Every, every_ms: 60_000_i64),
        payload: Autobot::Cron::CronPayload.new(message: "Use <tool> to check"),
      )

      result = channel.test_format_cron_job_html(job, 1)
      result.should_not contain("<script>")
      result.should contain("&lt;script&gt;")
      result.should contain("&lt;tool&gt;")
    ensure
      FileUtils.rm_rf(tmp) if tmp
    end
  end

  describe "#prepend_reply_context" do
    it "returns content unchanged when reply_text is nil" do
      channel = build_channel
      channel.test_prepend_reply_context("hello", nil).should eq("hello")
    end

    it "returns content unchanged when reply_text is empty" do
      channel = build_channel
      channel.test_prepend_reply_context("hello", "").should eq("hello")
    end

    it "prepends reply context" do
      channel = build_channel
      result = channel.test_prepend_reply_context("yes", "Do you agree?")
      result.should eq("[Replying to: \"Do you agree?\"]\n\nyes")
    end

    it "truncates long reply text" do
      channel = build_channel
      long_text = "a" * 600
      result = channel.test_prepend_reply_context("ok", long_text)
      result.should contain("[Replying to: \"#{"a" * 500}...\"]")
      result.should end_with("\n\nok")
    end

    it "does not truncate text at exactly max length" do
      channel = build_channel
      exact_text = "a" * 500
      result = channel.test_prepend_reply_context("ok", exact_text)
      result.should_not contain("...")
      result.should contain(exact_text)
    end
  end

  describe "#build_content_and_media" do
    it "uses typed text as the message and keeps a captioned file as an attachment" do
      channel = build_channel
      channel.stubbed_file_bytes = "bytes".to_slice
      msg = JSON.parse(%({"caption": "Add to notes", "audio": {"file_id": "a1", "mime_type": "audio/mp4", "title": "Memo"}}))

      content, media = channel.test_build_content_and_media(msg)

      content.should eq("Add to notes")
      media.size.should eq(1)
      media.first.type.should eq("audio")
      media.first.sender_voice_note?.should be_false
    end

    it "labels media when nothing was typed" do
      channel = build_channel
      channel.stubbed_file_bytes = "bytes".to_slice
      msg = JSON.parse(%({"document": {"file_id": "d1", "file_name": "report.pdf"}}))

      content, _ = channel.test_build_content_and_media(msg)

      content.should eq("[document: report.pdf]")
    end

    it "reports an empty message when there is neither text nor media" do
      content, media = build_channel.test_build_content_and_media(JSON.parse("{}"))
      content.should eq("[empty message]")
      media.should be_empty
    end

    it "renders media placeholders when message is forwarded without typed text" do
      channel = build_channel
      channel.stubbed_file_bytes = "bytes".to_slice
      msg = JSON.parse(<<-JSON
        {
          "forward_origin": {
            "type": "user",
            "sender_user": {"id": 1, "is_bot": false, "first_name": "Bob"}
          },
          "voice": {"file_id": "v1", "duration": 3}
        }
      JSON
      )

      content, media = channel.test_build_content_and_media(msg)
      content.should eq("[Forwarded from: Bob]\n[voice message]")
      media.size.should eq(1)
      media.first.origin.should eq("forwarded")
    end

    it "extracts user forward origin" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "forward_origin": {
            "type": "user",
            "sender_user": {"first_name": "Alice"}
          },
          "text": "Hello world"
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Forwarded from: Alice]\nHello world")
    end

    it "extracts hidden_user forward origin" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "forward_origin": {
            "type": "hidden_user",
            "sender_user_name": "SecretSender"
          },
          "text": "Confidential"
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Forwarded from: SecretSender]\nConfidential")
    end

    it "extracts chat forward origin with author signature" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "forward_origin": {
            "type": "chat",
            "sender_chat": {"title": "Dev Group"},
            "author_signature": "Lead Dev"
          },
          "text": "Update released"
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Forwarded from: Dev Group (Lead Dev)]\nUpdate released")
    end

    it "extracts channel forward origin without author signature" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "forward_origin": {
            "type": "channel",
            "chat": {"title": "News Channel"}
          },
          "text": "Breaking news"
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Forwarded from: News Channel]\nBreaking news")
    end

    it "extracts story content when id and chat are present" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "story": {
            "id": 42,
            "chat": {"title": "Travel Blog"}
          },
          "text": "On the beach"
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Story from Travel Blog (ID: 42)]\nOn the beach")
    end

    it "extracts story content when only id is present" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "story": {"id": 99},
          "text": "Look at this"
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Story (ID: 99)]\nLook at this")
    end

    it "extracts link preview options when URL is not in typed text" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "text": "Check out this documentation",
          "link_preview_options": {
            "url": "https://example.com/docs"
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("Check out this documentation\n[Link: https://example.com/docs]")
    end

    it "ignores link preview options when disabled" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "text": "Read the article",
          "link_preview_options": {
            "url": "https://example.com/article",
            "is_disabled": true
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("Read the article")
    end

    it "ignores link preview options when URL is already in typed text" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "text": "Visit https://example.com for more information",
          "link_preview_options": {
            "url": "https://example.com"
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("Visit https://example.com for more information")
    end

    it "extracts poll questions and options" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "poll": {
            "question": "What is your favorite language?",
            "options": [
              {"text": "Crystal"},
              {"text": "Rust"},
              {"text": "Go"}
            ]
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Poll: What is your favorite language?]\n- Crystal\n- Rust\n- Go")
    end

    it "extracts venue information with coordinates" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "venue": {
            "title": "Cafe Central",
            "address": "Av. Juarez 123",
            "location": {"latitude": 20.6597, "longitude": -103.3496}
          },
          "location": {"latitude": 20.6597, "longitude": -103.3496}
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Venue: Cafe Central, Av. Juarez 123 (20.659700, -103.349600)]")
    end

    it "extracts standalone location coordinates" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "location": {"latitude": 20.6597, "longitude": -103.3496}
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Location: 20.659700, -103.349600]")
    end

    it "extracts contact with name and phone" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "contact": {
            "first_name": "Bob",
            "last_name": "Smith",
            "phone_number": "+15551234"
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Contact: Bob Smith (+15551234)]")
    end

    it "extracts contact with phone only" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "contact": {
            "phone_number": "+15551234"
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Contact: unknown (+15551234)]")
    end

    it "extracts contact with name only" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "contact": {
            "first_name": "Bob"
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[Contact: Bob]")
    end

    it "reports an empty message for empty contact payload" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "contact": {}
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should eq("[empty message]")
    end

    it "extracts Bot API rich_message blocks (blockquote, pre with language, list)" do
      channel = build_channel
      msg = JSON.parse(<<-JSON
        {
          "rich_message": {
            "blocks": [
              {
                "type": "heading",
                "text": "Article Header"
              },
              {
                "type": "blockquote",
                "blocks": [
                  {"type": "paragraph", "text": "Quoted paragraph inside blockquote"}
                ]
              },
              {
                "type": "pre",
                "language": "crystal",
                "text": "puts 'hello'"
              },
              {
                "type": "list",
                "items": [
                  {
                    "label": "1.",
                    "blocks": [{"type": "paragraph", "text": "First item"}]
                  },
                  {
                    "label": "2.",
                    "blocks": [{"type": "paragraph", "text": "Second item"}]
                  }
                ]
              }
            ]
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should contain("### Article Header")
      content.should contain("[Quoting: \"Quoted paragraph inside blockquote\"]")
      content.should contain("```crystal\nputs 'hello'\n```")
      content.should contain("1. First item\n2. Second item")
    end

    it "preserves pre code block leading indentation in rich messages" do
      channel = build_channel
      msg = JSON.parse(<<-'JSON'
        {
          "rich_message": {
            "blocks": [
              {
                "type": "pre",
                "language": "python",
                "text": "    def test():\n        return 42"
              }
            ]
          }
        }
      JSON
      )

      content, _ = channel.test_build_content_and_media(msg)
      content.should contain("```python\n    def test():\n        return 42\n```")
    end
  end

  describe "#extract_reply_context" do
    it "prefers quote text over full reply_to_message text" do
      msg = JSON.parse(<<-JSON
        {
          "text": "why?",
          "quote": {"text": "selected quote"},
          "reply_to_message": {"text": "Full long message with selected quote inside"}
        }
      JSON
      )
      channel = build_channel
      channel.test_extract_reply_context(msg).should eq("selected quote")
    end

    it "returns nil when no reply_to_message" do
      msg = JSON.parse(%({"text": "hello"}))
      channel = build_channel
      channel.test_extract_reply_context(msg).should be_nil
    end

    it "extracts text from reply_to_message" do
      msg = JSON.parse(%({"text": "yes", "reply_to_message": {"text": "Do you want to proceed?"}}))
      channel = build_channel
      channel.test_extract_reply_context(msg).should eq("Do you want to proceed?")
    end

    it "extracts caption from reply_to_message" do
      msg = JSON.parse(%({"text": "nice", "reply_to_message": {"caption": "Here is the photo"}}))
      channel = build_channel
      channel.test_extract_reply_context(msg).should eq("Here is the photo")
    end

    it "returns nil when reply_to_message has no text or caption" do
      msg = JSON.parse(%({"text": "hello", "reply_to_message": {"message_id": 123}}))
      channel = build_channel
      channel.test_extract_reply_context(msg).should be_nil
    end

    it "returns empty string when reply_to_message text is empty" do
      msg = JSON.parse(%({"text": "hello", "reply_to_message": {"text": ""}}))
      channel = build_channel
      channel.test_extract_reply_context(msg).should eq("")
    end

    it "returns full text without truncation" do
      long_text = "a" * 600
      msg = JSON.parse(%({"text": "ok", "reply_to_message": {"text": "#{long_text}"}}))
      channel = build_channel
      result = channel.test_extract_reply_context(msg)
      result.should eq(long_text)
    end
  end

  describe "#find_sendable_attachment" do
    it "returns nil for nil media" do
      channel = build_channel
      channel.test_find_sendable_attachment(nil).should be_nil
    end

    it "returns nil for empty media" do
      channel = build_channel
      channel.test_find_sendable_attachment([] of Autobot::Bus::MediaAttachment).should be_nil
    end

    it "returns nil when no attachment has data" do
      channel = build_channel
      media = [Autobot::Bus::MediaAttachment.new(type: "document", url: "file_id")]
      channel.test_find_sendable_attachment(media).should be_nil
    end

    it "returns first attachment with data regardless of type" do
      channel = build_channel
      media = [
        Autobot::Bus::MediaAttachment.new(type: "document", url: "file_id"),
        Autobot::Bus::MediaAttachment.new(type: "animation", data: "gifdata"),
      ]
      result = channel.test_find_sendable_attachment(media)
      result.should_not be_nil
      result.as(Autobot::Bus::MediaAttachment).type.should eq("animation")
    end
  end

  describe "#media_filename" do
    it "returns basename from file_path" do
      channel = build_channel
      attachment = Autobot::Bus::MediaAttachment.new(type: "animation", file_path: "output/my_animation.gif", data: "x")
      channel.test_media_filename(attachment, "default.gif").should eq("my_animation.gif")
    end

    it "returns default when no file_path" do
      channel = build_channel
      attachment = Autobot::Bus::MediaAttachment.new(type: "photo", data: "x")
      channel.test_media_filename(attachment, "image.png").should eq("image.png")
    end
  end

  describe "#build_media_multipart" do
    it "builds multipart body for animation" do
      channel = build_channel
      body = channel.test_build_media_multipart("123", "gif".to_slice, "A GIF",
        field_name: "animation", filename: "test.gif", content_type: "image/gif")

      body.should contain("name=\"chat_id\"")
      body.should contain("123")
      body.should contain("name=\"animation\"")
      body.should contain("filename=\"test.gif\"")
      body.should contain("Content-Type: image/gif")
      body.should contain("name=\"caption\"")
      body.should contain("A GIF")
    end

    it "builds multipart body for document" do
      channel = build_channel
      body = channel.test_build_media_multipart("456", "pdf".to_slice, "A PDF",
        field_name: "document", filename: "report.pdf", content_type: "application/pdf")

      body.should contain("name=\"document\"")
      body.should contain("filename=\"report.pdf\"")
      body.should contain("Content-Type: application/pdf")
    end
  end

  describe "#find_photo_attachment" do
    it "returns nil for nil media" do
      channel = build_channel
      channel.test_find_photo_attachment(nil).should be_nil
    end

    it "returns nil for empty media" do
      channel = build_channel
      channel.test_find_photo_attachment([] of Autobot::Bus::MediaAttachment).should be_nil
    end

    it "returns nil when no photo type" do
      channel = build_channel
      media = [Autobot::Bus::MediaAttachment.new(type: "document", data: "abc")]
      channel.test_find_photo_attachment(media).should be_nil
    end

    it "returns nil when photo has no data" do
      channel = build_channel
      media = [Autobot::Bus::MediaAttachment.new(type: "photo", url: "file_id")]
      channel.test_find_photo_attachment(media).should be_nil
    end

    it "returns photo attachment with data" do
      channel = build_channel
      attachment = Autobot::Bus::MediaAttachment.new(type: "photo", data: "base64data")
      media = [attachment]
      result = channel.test_find_photo_attachment(media)
      result.should_not be_nil
      found = result.as(Autobot::Bus::MediaAttachment)
      found.type.should eq("photo")
      found.data.should eq("base64data")
    end
  end

  describe "#build_photo_multipart" do
    it "builds multipart body with chat_id, photo, and caption" do
      channel = build_channel
      photo_bytes = "hello".to_slice
      body = channel.test_build_photo_multipart("123", photo_bytes, "A caption")

      body.should contain("name=\"chat_id\"")
      body.should contain("123")
      body.should contain("name=\"photo\"")
      body.should contain("filename=\"image.png\"")
      body.should contain("name=\"caption\"")
      body.should contain("A caption")
    end

    it "truncates caption longer than limit" do
      channel = build_channel
      photo_bytes = "x".to_slice
      long_caption = "a" * 2000
      body = channel.test_build_photo_multipart("123", photo_bytes, long_caption)

      # Caption should be truncated to PHOTO_CAPTION_LIMIT (1024)
      caption_section = body.split("name=\"caption\"").last
      # The caption content (between headers and boundary) should be truncated
      caption_section.size.should be < 2000
    end
  end

  describe "#parse_script_args" do
    it "correctly parses simple arguments" do
      channel = build_channel
      channel.test_parse_script_args("foo bar baz").should eq(["foo", "bar", "baz"])
    end

    it "handles double quotes" do
      channel = build_channel
      channel.test_parse_script_args("foo \"bar baz\"").should eq(["foo", "bar baz"])
    end

    it "handles single quotes" do
      channel = build_channel
      channel.test_parse_script_args("foo 'bar baz'").should eq(["foo", "bar baz"])
    end

    it "handles escaped spaces" do
      channel = build_channel
      channel.test_parse_script_args("foo bar\\ baz").should eq(["foo", "bar baz"])
    end

    it "handles empty string" do
      channel = build_channel
      channel.test_parse_script_args("").should eq([] of String)
    end
  end

  describe "#execute_script" do
    it "drains large stderr output concurrent with stdout without deadlocking" do
      tmp = TestHelper.tmp_dir
      script_file = tmp / "test_script.sh"
      File.write(script_file, <<-SCRIPT)
        #!/bin/sh
        echo 'stdout output'
        yes x | head -c 100000 >&2
        exit 1
        SCRIPT
      File.chmod(script_file, 0o755)

      channel = build_channel
      channel.test_execute_script(script_file.to_s, "", "123")

      channel.sent_replies.size.should eq(1)
      channel.sent_replies.first.should contain("Script failed (exit 1)")
      channel.sent_replies.first.should contain("truncated")
    ensure
      FileUtils.rm_rf(tmp) if tmp
    end
  end

  describe "#apply_proxy" do
    it "leaves the client unproxied when no proxy is configured" do
      channel = build_channel
      client = HTTP::Client.new(URI.parse("http://127.0.0.1"))

      channel.test_apply_proxy(client)

      client.proxy?.should be_false
    end

    it "leaves the client unproxied when the proxy URL has no host" do
      channel = build_channel(proxy: "invalid")
      client = HTTP::Client.new(URI.parse("http://127.0.0.1"))

      channel.test_apply_proxy(client)

      client.proxy?.should be_false
    end

    it "connects the client through the configured proxy" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port
      channel = build_channel(proxy: "http://127.0.0.1:#{port}")
      # Plain-http target: the proxy connection opens without a CONNECT handshake.
      client = HTTP::Client.new(URI.parse("http://upstream.test"))

      channel.test_apply_proxy(client)

      if proxy = client.proxy
        proxy.host.should eq("127.0.0.1")
        proxy.port.should eq(port)
      else
        fail("expected proxy to be applied to the client")
      end
    ensure
      client.close if client
      server.close if server
    end
  end

  describe "#send_media_request" do
    it "tunnels the media upload through the configured proxy" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port
      connect_request = Channel(String).new

      spawn do
        if socket = server.accept?
          request_line = socket.gets.to_s
          while (line = socket.gets) && !line.empty?
          end
          socket << "HTTP/1.1 403 Forbidden\r\nContent-Length: 0\r\n\r\n"
          socket.flush
          socket.close
          connect_request.send(request_line)
        end
      end

      channel = build_channel(proxy: "http://127.0.0.1:#{port}")
      upload_error = Channel(Exception?).new

      spawn do
        channel.test_send_media_request("123", Bytes[1, 2, 3], "caption",
          api_method: "sendPhoto", field_name: "photo",
          filename: "image.png", content_type: "image/png")
        upload_error.send(nil)
      rescue ex
        upload_error.send(ex)
      end

      select
      when error = upload_error.receive
        error.should be_a(IO::Error)
      when timeout(5.seconds)
        fail("media upload did not go through the proxy")
      end

      select
      when request_line = connect_request.receive
        request_line.should eq("CONNECT api.telegram.org:443 HTTP/1.1")
      when timeout(5.seconds)
        fail("proxy did not receive a CONNECT request")
      end
    ensure
      server.close if server
    end
  end

  describe "HTTP client socket lifecycle" do
    it "closes client in #api_request on successful keep-alive response" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port

      spawn do
        if socket = server.accept?
          while (line = socket.gets) && !line.empty?
          end
          body = %({"ok":true,"result":{"id":123,"is_bot":true,"first_name":"bot"}})
          socket << "HTTP/1.1 200 OK\r\nContent-Type: application/json\r\nConnection: keep-alive\r\nContent-Length: #{body.bytesize}\r\n\r\n#{body}"
          socket.flush
        end
      end

      channel = build_channel
      channel.target_uri = URI.parse("http://127.0.0.1:#{port}")
      result = channel.test_api_request("getMe")

      result.should_not be_nil
      result.try(&.[]("id").as_i).should eq(123)
      channel.created_clients.size.should eq(1)
      channel.created_clients.first.explicitly_closed?.should be_true
    ensure
      server.close if server
    end

    it "closes client in #api_request on network error" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port

      spawn do
        while socket = server.accept?
          socket.close
        end
      end

      channel = build_channel(proxy: "http://127.0.0.1:#{port}")
      channel.test_api_request("getMe")

      channel.created_clients.size.should eq(1)
      channel.created_clients.first.explicitly_closed?.should be_true
    ensure
      server.close if server
    end

    it "closes client in #api_get on network error" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port

      spawn do
        while socket = server.accept?
          socket.close
        end
      end

      channel = build_channel(proxy: "http://127.0.0.1:#{port}")
      channel.test_api_get("getUpdates")

      channel.created_clients.size.should eq(1)
      channel.created_clients.first.explicitly_closed?.should be_true
    ensure
      server.close if server
    end

    it "closes download client in #download_telegram_file_bytes on network error" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port

      spawn do
        while socket = server.accept?
          socket.close
        end
      end

      channel = build_channel(proxy: "http://127.0.0.1:#{port}")
      channel.stubbed_file_path = "photos/file_123.jpg"
      channel.test_download_telegram_file_bytes("file_123")

      channel.created_clients.size.should eq(1)
      channel.created_clients.first.explicitly_closed?.should be_true
    ensure
      server.close if server
    end

    it "closes client in #send_media_request on network error" do
      server = TCPServer.new("127.0.0.1", 0)
      port = server.local_address.port

      spawn do
        while socket = server.accept?
          socket.close
        end
      end

      channel = build_channel(proxy: "http://127.0.0.1:#{port}")
      begin
        channel.test_send_media_request("123", Bytes[1, 2, 3], "caption",
          api_method: "sendPhoto", field_name: "photo",
          filename: "image.png", content_type: "image/png")
      rescue
      end

      channel.created_clients.should_not be_empty
      channel.created_clients.all?(&.explicitly_closed?).should be_true
    ensure
      server.close if server
    end
  end

  describe "#send_message" do
    it "ignores empty or whitespace-only messages without sending requests" do
      channel = build_channel
      outbound = Autobot::Bus::OutboundMessage.new(
        channel: "telegram",
        chat_id: "123",
        content: "   \n  "
      )

      channel.send_message(outbound)
      channel.created_clients.should be_empty
    end
  end
end
